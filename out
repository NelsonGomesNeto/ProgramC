Code -> DeclFuncOpt Code
Code -> DeclFuncOpt
DeclFuncOpt -> 'entryPoint' DeclFunc
DeclFuncOpt -> DeclFunc
DeclFunc -> 'function' 'id' 'opParen' 'clParen' 'isType' Type Body
DeclFunc -> 'function' 'id' 'opParen' FuncDeclVar 'clParen' 'isType' Type Body
DeclFunc -> 'function' 'id' 'opParen' 'clParen' Body
DeclFunc -> 'function' 'id' 'opParen' FuncDeclVar 'clParen' Body
FuncDeclVar -> IdsList 'isType' Type 'comma' FuncDeclVar
FuncDeclVar -> IdsList 'isType' Type
DeclVars -> IdsList 'isType' Type
ParamList -> Expr 'comma' ParamList
ParamList -> Expr
IdsList -> 'id' 'comma' IdsList
IdsList -> 'id'
Literal -> 'bool'
Literal -> 'int'
Literal -> 'real'
Literal -> 'scynot'
Literal -> 'char'
Literal -> 'string'
Value -> Literal
Value -> FuncCall
Value -> Array
Value -> 'id'
Type -> 'typeBool'
Type -> 'typeInt'
Type -> 'typeReal'
Type -> 'typeChar'
Type -> 'typeString'
Type -> TypeArray
TypeArray -> 'typeArray' 'opBrackets' 'clBrackets' 'of' Type
TypeArray -> 'typeArray' 'opBrackets' Expr 'clBrackets' 'of' Type
Array -> 'id' ArrayPos
ArrayPos -> 'opBrackets' Expr 'clBrackets' ArrayPos
ArrayPos -> 'opBrackets' Expr 'clBrackets'
Body -> 'opBraces' Lines 'clBraces'
Body -> 'opBraces' 'clBraces'
Lines -> Line Lines
Lines -> Line
Lines -> 'returnFun' Expr
Lines -> 'returnFun'
Line -> Statement 'comma' Line
Line -> Statement
Statement -> DeclVars
Statement -> FuncCall
Statement -> If
Statement -> Loop
Statement -> Attrib
Attrib -> LeftAttrib 'attrib' Expr
LeftAttrib -> 'id'
LeftAttrib -> Array
If -> 'ifSel' 'opParen' Expr 'clParen' Body
If -> 'ifSel' 'opParen' Expr 'clParen' Body ElIfAux
ElIfAux -> 'elseSel' Body
ElIfAux -> 'elseSel' If
ElIfAux -> 'elifSel' 'opParen' Expr 'clParen' ElIf
ElIf -> Body ElIfAux
ElIf -> Body
Expr -> Expr 'logicOr' ExprAnd
Expr -> ExprAnd
ExprAnd -> ExprAnd 'logicAnd' ExprBor
ExprAnd -> ExprBor
ExprBor -> ExprBor 'bitOr' ExprBxor
ExprBor -> ExprBxor
ExprBxor -> ExprBxor 'bitXor' ExprBand
ExprBxor -> ExprBand
ExprBand -> ExprBand 'bitAnd' ExprEq
ExprBand -> ExprEq
ExprEq -> ExprEq 'eqOrDiff' ExprRel
ExprEq -> ExprRel
ExprRel -> ExprRel 'relational' ExprShift
ExprRel -> ExprShift
ExprShift -> ExprShift 'bitShift' ExprAdd
ExprShift -> ExprAdd
ExprAdd -> ExprAdd 'plus' ExprMult
ExprAdd -> ExprAdd 'minus' ExprMult
ExprAdd -> ExprMult
ExprMult -> ExprMult 'mult' ExprPow
ExprMult -> ExprPow
ExprPow -> ExprPow 'exp' ExprUnary
ExprPow -> ExprUnary
ExprUnary -> 'minus' ExprCast
ExprUnary -> 'unary' ExprCast
ExprUnary -> ExprCast
ExprCast -> ExprFac 'asCast' Type
ExprCast -> ExprFac
ExprFac -> Value
ExprFac -> 'opParen' Expr 'clParen'
FuncCall -> 'id' 'opParen' ParamList 'clParen'
FuncCall -> 'id' 'opParen' 'clParen'
Loop -> 'repeat' 'whileLoop' 'opParen' Expr 'clParen' Body
Loop -> 'repeat' 'opParen' 'id' 'attrib' Expr 'to' Expr 'clParen' Body
